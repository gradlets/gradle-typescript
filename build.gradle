/*
 * (c) Copyright 2021 Felipe Orozco, Robert Kruszewski. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }

    dependencies {
        classpath 'com.gradle.publish:plugin-publish-plugin:0.14.0'
        classpath 'com.netflix.nebula:gradle-info-plugin:9.3.0'
        classpath 'com.netflix.nebula:nebula-publishing-plugin:17.3.2'
        classpath 'com.palantir.baseline:gradle-baseline-java:3.74.0'
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:1.28.0'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:0.12.3'
        classpath 'com.palantir.javaformat:gradle-palantir-java-format:2.0.1'
        classpath 'gradle.plugin.org.inferred:gradle-processors:3.3.0'
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.4.18" apply false
}

apply plugin: 'com.palantir.baseline'
apply plugin: 'com.palantir.consistent-versions'
apply plugin: 'com.palantir.git-version'

version System.env.CIRCLE_TAG ?: gitVersion()

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'com.palantir.java-format'
    apply plugin: 'org.inferred.processors'

    version rootProject.version
    group 'com.gradlets.gradle.typescript'

    repositories {
        mavenCentral()
        gradlePluginPortal()
        maven { url 'https://dl.bintray.com/marshallpierce/maven/' }
    }

    java {
        sourceCompatibility JavaVersion.VERSION_11
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-Werror']
        options.errorprone.disable 'SafeLoggingExceptionMessageFormat'
    }

    tasks.withType(Test) {
        testLogging {
            events 'failed', 'skipped', 'passed'
        }
    }

    tasks.check.dependsOn javadoc, checkUnusedDependencies
}
